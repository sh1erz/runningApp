//Android
ext {
    androidMinSdkVersion = 28
    androidTargetSdkVersion = 30
    androidCompileSdkVersion = 33
}

//Libraries
ext {

    // Android X
    androidxVersion = '1.9.0'
    constraintlayoutVersion = '2.1.4'

    // Hilt
    hiltVersion = '2.44'

    //Room
    roomVersion = "2.5.0"

    //Navigation
    navVersion = "2.5.3"

    // Remote
    okhttp3Version = '4.9.3'
    gsonVersion = '2.9.0'
    retrofitVersion = '2.9.0'

    //Java
    javaxAnnotationVersion = '1.0'
    javaxInjectVersion = '1'

    // Kotlin
    kotlinVersion = '1.3.41'
    ktxVersion = '1.2.0-alpha03'

    //Coroutines
    coroutinesVersion = '1.5.1'

    // Logs + Crash Reporting
    timberVersion = '4.7.0'

}

def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

ext.appDependencies = dependencyGroup {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(":core")
    implementation project(":framework")

    implementation project(":feature-core")
    implementation project(":feature-main")

    commonDeps()
    ui()
    auth()
    implementation 'com.google.firebase:firebase-bom:31.2.2'
}

ext.featureCoreDeps = dependencyGroup {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":core")

    commonDeps()
    ui()
    auth()
}

ext.featureDeps = dependencyGroup {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":feature-core")
    implementation project(":core")

    commonDeps()
    ui()
}

ext.coreDeps = dependencyGroup {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    hiltDeps()
    commonDeps()
}

ext.frameworkDeps = dependencyGroup {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":core")

    implementation 'com.google.firebase:firebase-firestore-ktx:24.4.5'
    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"

    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'

    room()
    hiltDeps()
    commonDeps()
}

ext.ui = dependencyGroup {
    hiltDeps()

    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"

    implementation "androidx.core:core-ktx:$androidxVersion"
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.8.0'
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
}

ext.commonDeps = dependencyGroup {
    implementation 'com.jakewharton.timber:timber:5.0.1'
    testImplementation 'junit:junit:4.13.2'
}

ext.hiltDeps = dependencyGroup {
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"
}

ext.gms = dependencyGroup {
    implementation 'com.google.android.gms:play-services-maps:18.1.0'
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'com.google.maps.android:android-maps-utils:2.2.3'
    implementation 'com.google.maps.android:maps-utils-ktx:3.2.0'
}

ext.auth = dependencyGroup {
    implementation 'com.google.firebase:firebase-auth-ktx:21.1.0'
    implementation 'com.google.android.gms:play-services-auth:20.4.1'
}

ext.room = dependencyGroup {
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    // Paging 3 Integration
    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.room:room-paging:$roomVersion"

    testImplementation "androidx.room:room-testing:$roomVersion"
}